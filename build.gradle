buildscript {
    final majorVersion = 0
    final minorVersion = 1
    final patchVersion = 0

    final latestSdk = 28

    ext.config = [
            // Major -> Millions, Minor -> Thousands, Patch -> Hundreds. E.g 1.3.72 == 1,003,072
            versionCode: 1000000 * majorVersion + 1000 * minorVersion + patchVersion,
            versionName: "$majorVersion.$minorVersion.$patchVersion",

            minSdk     : 19,
            compileSdk : latestSdk,
            targetSdk  : latestSdk
    ]

    final supportVersion = '1.0.0-beta01'

    final versions = [
            kotlin          : '1.2.61',
            appcompat       : supportVersion,
            annotation      : supportVersion,
            material        : supportVersion,
            ktx             : supportVersion,
            constraintlayout: '2.0.0-alpha1',
            junit           : '4.12',
            atsl            : '1.1.0-alpha3', // Android Testing Support Library
            espresso        : '3.1.0-alpha3'
    ]

    ext.deps = [
            kotlinGradlePlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            kotlinStdLib      : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
            appcompat         : "androidx.appcompat:appcompat:${versions.appcompat}",
            material          : "com.google.android.material:material:${versions.material}",
            annotation        : "androidx.annotation:annotation:${versions.annotation}",
            ktx               : "androidx.core:core-ktx:${versions.ktx}",
            constraintlayout  : "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
            junit             : "junit:junit:${versions.junit}",
            atsl              : "androidx.test:runner:${versions.atsl}",
            espresso          : "androidx.test.espresso:espresso-core:${versions.espresso}"
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha03'
        classpath deps.kotlinGradlePlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
